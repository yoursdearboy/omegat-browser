plugins {
    id 'java'
    id 'distribution'
    id 'groovy'
    id "org.openjfx.javafxplugin" version "0.0.9"
    id "org.omegat.gradle"        version "1.4.2"
}

group 'com.yoursdearboy.omegat.plugins.browser'
version '1.4.0-rc.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

omegat {
    version = "5.2.0"
    pluginClass = "com.yoursdearboy.omegat.plugins.browser.BrowserPlugin"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.codehaus.groovy:groovy-all:3.0.1'
    testImplementation group: 'junit', name: 'junit', version: '4.11'
}

javafx {
    version = '11'
    modules = [ 'javafx.swing', 'javafx.web']
    configuration = 'packIntoJar'
}

task copyToOmegaT {
    doLast {
        def VAR_NAME = "OMEGAT_HOME"
        def omegatDir = System.getenv(VAR_NAME)
        if (!omegatDir) omegatDir = System.getProperty(VAR_NAME)
        if (!omegatDir) throw new GradleException("Set $VAR_NAME variable or property")
        copy {
            from file("$buildDir/libs")
            into file("$omegatDir/plugins")
        }
    }
}

distTar {
    compression = Compression.GZIP
}

artifacts {
    archives jar
}

distributions {
    main {
        contents {
            from(jar)
            from('README.md')
        }
    }
}
